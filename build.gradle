plugins {
	id 'fabric-loom' version '0.7-SNAPSHOT'
	id 'maven-publish'
	id "com.jfrog.bintray" version "1.8.4"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
	accessWidener =  file("src/main/resources/customportalapi.accesswidener")
}
repositories {
}
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	//mappings minecraft.officialMojangMappings()
	mappings "net.fabricmc:yarn:${project.yarn_mappings}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

}
bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['bintrayPublication']
	pkg {
		repo = 'customportalapi'
		name = 'customportalapi'
		userOrg = 'kyrptonaught'
		licenses = ['MIT']
		vcsUrl = 'https://github.com/kyrptonaught/customportalapi'
		version {
			name = project.version
			released  = new Date()
			vcsTag = project.version
		}
	}
}
processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		bintrayPublication(MavenPublication) {
			groupId 'net.kyrptonaught'
			artifactId 'customportalapi'
			version project.version

			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		mavenLocal()
	}
}
